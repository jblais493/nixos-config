#+title: Joshua Blais' Nix Configuration

[[https://nixos.org][https://img.shields.io/badge/NixOS-unstable-blue.svg?style=flat-square&logo=NixOS&logoColor=white]]

#+BEGIN_QUOTE
*Disclaimer:* I have no idea what I am doing, so take all of this with a grain (massive boulder) of salt.
#+END_QUOTE


* Introduction
This is a literate file using [[https://orgmode.org/][org-mode]] to [[https://orgmode.org/manual/Extracting-Source-Code.html][tangle]] all code blocks to various *.nix files across the configuration. I am used to one, massive (partially) organized file (see my [[https://github.com/jblais493/nixos-config/blob/master/dotfiles/doom/config.org][Emacs Configuration]]), so this seemed natural: Documentation and configuration all in one file? How nice.

What this ends up being is, quite literally, the *One File to Rule Them All*: My *entire computing landscape is declared* right where you are looking. This is the power of Nix.

[[https://nixos.org/][NixOS]] is what I would refer to as "the endgame of systems management". I have used various distributions of Linux in the past, and while I have no gripes with the likes of Debian, Fedora, or Arch, I have always forgotten configuration settings in the past, struggled to maintain multiple machines without drift, or had breaking changes on each and every distro.

NixOS offers the bleeding edge with the stable, allowing easy rollbacks of any issues, allowing me to  continue on with work and then fix things at a later date if I have a breaking change. This is increasingly important as I host various projects and services for a broader audience and cannot deal with any downtime.

What makes this possible is:
- [[https://nixos.org/][NixOS]]
- [[https://github.com/nix-community/home-manager][home-manager]]
- [[https://github.com/serokell/deploy-rs][deploy-rs]]
- [[https://github.com/nix-community/nixos-anywhere][nixos-anywhere]]
- [[https://orgmode.org/][org-mode]] (this document)

Excited yet? Let us begin...
** Setting this all up
*** Local Installation (Laptops/Desktops)
1. Obtain [[https://nixos.org/download/][installation media for nixOS]].
2. Flash .iso to flash drive using ~dd~ or similar tool:
   #+begin_src bash
   dd if=nixos-minimal-xx.xx.iso of=/dev/sdX bs=4M status=progress
   #+end_src
3. Boot up installer and connect to internet
4. Partition your drives (example for UEFI systems):
   #+begin_src bash
   # Create partitions
   parted /dev/nvme0n1 -- mklabel gpt
   parted /dev/nvme0n1 -- mkpart primary 512MiB -8GiB
   parted /dev/nvme0n1 -- mkpart primary linux-swap -8GiB 100%
   parted /dev/nvme0n1 -- mkpart ESP fat32 1MiB 512MiB
   parted /dev/nvme0n1 -- set 3 esp on

   # Format partitions
   mkfs.ext4 -L nixos /dev/nvme0n1p1
   mkswap -L swap /dev/nvme0n1p2
   mkfs.fat -F 32 -n boot /dev/nvme0n1p3

   # Mount filesystems
   mount /dev/disk/by-label/nixos /mnt
   mkdir -p /mnt/boot
   mount /dev/disk/by-label/boot /mnt/boot
   swapon /dev/nvme0n1p2
   #+end_src
5. Generate hardware configuration:
   #+begin_src bash
   nixos-generate-config --root /mnt
   #+end_src
6. Clone this repository:
   #+begin_src bash
   cd /mnt/home
   git clone https://github.com/jblais493/nixos-config.git
   #+end_src
7. Copy the generated hardware configuration:
   #+begin_src bash
   cp /mnt/etc/nixos/hardware-configuration.nix /mnt/home/nixos-config/hosts/YOUR_HOST/
   #+end_src
8. Edit the host configuration to match your setup:
   #+begin_src bash
   # Edit hostname and any host-specific settings
   nano /mnt/home/nixos-config/hosts/YOUR_HOST/configuration.nix
   #+end_src
9. Test the configuration:
   #+begin_src bash
   cd /mnt/home/nixos-config
   nix flake check
   #+end_src
10. Install NixOS:
    #+begin_src bash
    nixos-install --flake .#YOUR_HOST
    #+end_src
11. Reboot and enjoy your declarative system!

*** Installation on VPS/headless servers
For automated server deployments, use [[https://nix-community.github.io/nixos-anywhere/quickstart.html][nixos-anywhere]].

Quick server setup:
#+begin_src bash
# Install nixos-anywhere
nix run github:nix-community/nixos-anywhere -- \
  --flake .#YOUR_SERVER_HOST \
  root@your-server-ip
#+end_src

* Dotfiles
This directory contains all dotfiles for the programs and tooling I use. I previously used [[https://www.gnu.org/software/stow/][Stow]] to organize all these config files, but now use home-manager's ~mkOutOfStoreSymlink~ to sym link and keep these files organized within this one config - simplification is beautiful. See the section on the home-manager module below.

#+begin_src bash
❯ tree -d -L 2
├── doom
│   ├── lisp
│   ├── private
│   ├── snippets
│   ├── templates
│   └── themes
├── hypr
├── kitty
├── mpd
│   └── playlists
├── nvim
│   ├── lua
│   ├── scripts
│   └── snippets
├── +STORE
│   └── dictionary
├── swaync
│   └── styles
├── tmux
│   └── tmux
├── waybar
│   ├── modules
│   └── scripts
├── wofi
├── zathura
└── zsh
#+end_src

* Hosts
** Theologica
*** README.md
#+begin_src markdown :tangle hosts/theologica/README.md
# Theologica

My daily driver machine, a Lenovo T14s gen 3 laptop.

## Specifications
- Model: Lenovo T14s Gen 3
- Purpose: Daily development and writing work
- Desktop: Hyprland
#+end_src
*** configuration.nix
This declares all imported modules and specific tweaks to the Theologica host:
#+begin_src nix :tangle hosts/theologica/configuration.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/desktop
    ../../modules/shared
    ../../modules/cli-tui
    ../../modules/development
    ../../modules/media
    ../../modules/security
  ];

  # Host-specific configuration
  networking.hostName = "theologica";

  # Boot loader configuration
  boot.loader.systemd-boot.enable = true;
  boot.loader.efi.canTouchEfiVariables = true;

  # Define your user properly
  users.users.joshua = {
    isNormalUser = true;
    description = "Joshua Blais";
    group = "joshua";
    extraGroups = [ "networkmanager" "wheel" ];
  };

  # Create the user group
  users.groups.joshua = {};

  # Basic system configuration
  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

  # Set the state version
  system.stateVersion = "25.05";
}
#+end_src
*** hardware-configuration.nix
#+begin_src nix :tangle hosts/theologica/hardware-configuration.nix
# This is a placeholder hardware configuration
# You'll need to generate the real one when you install NixOS
# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{ config, lib, pkgs, modulesPath, ... }:

{
  imports =
    [ (modulesPath + "/installer/scan/not-detected.nix")
    ];

  boot.initrd.availableKernelModules = [ "nvme" "xhci_pci" "thunderbolt" "usb_storage" "sd_mod" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-amd" ];
  boot.extraModulePackages = [ ];

  fileSystems."/" =
    { device = "/dev/disk/by-uuid/14d03f28-59d0-4e29-8ea0-fec882ed5eac";
      fsType = "ext4";
    };

  boot.initrd.luks.devices."luks-08dfaa11-224c-4623-a507-656a67258b8c".device = "/dev/disk/by-uuid/08dfaa11-224c-4623-a507-656a67258b8c";

  fileSystems."/boot" =
    { device = "/dev/disk/by-uuid/23D9-F036";
      fsType = "vfat";
      options = [ "fmask=0077" "dmask=0077" ];
    };

  swapDevices = [ ];

  # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
  # (the default) this is the recommended approach. When using systemd-networkd it's
  # still possible to use this option, but it's recommended to use it in conjunction
  # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
  networking.useDHCP = lib.mkDefault true;
  # networking.interfaces.enp53s0f4u1u4u3.useDHCP = lib.mkDefault true;
  # networking.interfaces.wlp1s0.useDHCP = lib.mkDefault true;
  # networking.interfaces.wwan0.useDHCP = lib.mkDefault true;

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.amd.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
#+end_src

** King
*** README.md
#+begin_src markdown :tangle hosts/king/README.md
# King

My testing machine, a Lenovo T430 laptop.

## Specifications
- Model: Lenovo T430
- Purpose: Testing configurations before deploying to main machine
- Desktop: Hyprland (testing)
#+end_src

*** configuration.nix
This declares all imported modules and specific tweaks to the King host:
#+begin_src nix :tangle hosts/king/configuration.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/desktop
    ../../modules/shared
    ../../modules/cli-tui
    ../../modules/development
    ../../modules/media
  ];

  nix.settings.experimental-features = [ "nix-command" "flakes" ];

  boot.loader.grub = {
    enable = true;
    devices = [ "/dev/sda" ];
    useOSProber = true;
  };

  # Setup keyfile
  boot.initrd.secrets = {
    "/boot/crypto_keyfile.bin" = null;
  };

  boot.loader.grub.enableCryptodisk = true;
  boot.initrd.luks.devices."luks-89f9b5d7-d320-4b23-8db5-e3e5823e0578".keyFile = "/boot/crypto_keyfile.bin";
  networking.hostName = "king"; # Define your hostname.

  users.users.joshua = {
    isNormalUser = true;
    description = "Joshua Blais";
    group = "joshua";
    extraGroups = [ "networkmanager" "wheel" ];
  };

  users.groups.joshua = {};

  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

  environment.systemPackages = with pkgs; [
    vim
    git
  ];

  system.stateVersion = "24.11";
}
#+end_src
*** hardware-configuration.nix
#+begin_src nix :tangle hosts/king/hardware-configuration.nix
# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{ config, lib, pkgs, modulesPath, ... }:

{
  imports =
    [ (modulesPath + "/installer/scan/not-detected.nix")
    ];

  boot.initrd.availableKernelModules = [ "xhci_pci" "ehci_pci" "ahci" "usb_storage" "sd_mod" "sdhci_pci" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ ];
  boot.extraModulePackages = [ ];

  fileSystems."/" =
    { device = "/dev/disk/by-uuid/cb65c275-db79-4ef5-b231-54100efad99d";
      fsType = "ext4";
    };

  boot.initrd.luks.devices."luks-89f9b5d7-d320-4b23-8db5-e3e5823e0578".device = "/dev/disk/by-uuid/89f9b5d7-d320-4b23-8db5-e3e5823e0578";

  swapDevices = [ ];

  # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
  # (the default) this is the recommended approach. When using systemd-networkd it's
  # still possible to use this option, but it's recommended to use it in conjunction
  # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
  networking.useDHCP = lib.mkDefault true;
  # networking.interfaces.enp0s25.useDHCP = lib.mkDefault true;
  # networking.interfaces.wlp3s0.useDHCP = lib.mkDefault true;

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}

#+end_src
** Axios
*** README.md
#+begin_src markdown :tangle hosts/axios/README.md
# Axios

Aurelia's machine, a Lenovo X230 laptop.

## Specifications
- Model: Lenovo X230
- Purpose: Aurelia's daily driver
- Desktop: Hyprland
#+end_src

*** configuration.nix
This declares all imported modules and specific tweaks to the King host:
#+begin_src nix :tangle hosts/axios/configuration.nix
{ config, pkgs, lib, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/desktop
    ../../modules/shared
    ../../modules/cli-tui
    ../../modules/development
    ../../modules/media
    ../../modules/security
  ];

  # Bootloader.
  boot.loader.grub.enable = true;
  boot.loader.grub.device = "/dev/sda";
  boot.loader.grub.useOSProber = true;

  # Setup keyfile
  boot.initrd.secrets = {
    "/boot/crypto_keyfile.bin" = null;
  };

  boot.loader.grub.enableCryptodisk = true;

  boot.initrd.luks.devices."luks-68e37805-7090-4a03-a958-7c3271384a93".keyFile = "/boot/crypto_keyfile.bin";
  networking.hostName = "axios";
  nix.settings.experimental-features = [ "nix-command" "flakes" ];
  # networking.wireless.enable = true;  # Enables wireless support via wpa_supplicant.

  # Enable networking
  networking.networkmanager.enable = true;

  # Enable the X11 windowing system.
  services.xserver.enable = true;

  # Enable the GNOME Desktop Environment.
  # services.displayManager.gdm.enable = true;
  services.desktopManager.gnome.enable = true;

  # Configure keymap in X11
  services.xserver.xkb = {
    layout = "us";
    variant = "";
  };

# SSH configuration
services.openssh = {
  enable = true;
  settings = {
    PermitRootLogin = "no";  # Change this to "no" for security
    PasswordAuthentication = false;  # Enable password auth temporarily
  };
};

  networking.firewall.allowedTCPPorts = [ 22 ];

users.users.joshua = {
  isNormalUser = true;
  description = "joshua";
  extraGroups = [ "networkmanager" "wheel" "uinput" "input" ];
  openssh.authorizedKeys.keys = [
    "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAICCWNto66rFbOvb1VDEDuZYdwHQPfKM7+EjpnHvs3eRr joshua@joshuablais.com"
  ];
};

  environment.systemPackages = with pkgs; [
    btrfs-progs
    btrbk
    compsize
  ];

  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

  system.stateVersion = "25.05";
}
#+end_src

*** hardware-configuration.nix
#+begin_src nix :tangle hosts/axios/hardware-configuration.nix
# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{ config, lib, pkgs, modulesPath, ... }:

{
  imports =
    [ (modulesPath + "/installer/scan/not-detected.nix")
    ];

  boot.initrd.availableKernelModules = [ "xhci_pci" "ehci_pci" "ahci" "usb_storage" "sd_mod" "sdhci_pci" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-intel" ];
  boot.extraModulePackages = [ ];

  fileSystems."/" =
    { device = "/dev/disk/by-uuid/b2896044-4591-45ec-a462-eff16f2fbf5b";
      fsType = "ext4";
    };

  boot.initrd.luks.devices."luks-68e37805-7090-4a03-a958-7c3271384a93".device = "/dev/disk/by-uuid/68e37805-7090-4a03-a958-7c3271384a93";

  swapDevices = [ ];

  # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
  # (the default) this is the recommended approach. When using systemd-networkd it's
  # still possible to use this option, but it's recommended to use it in conjunction
  # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
  networking.useDHCP = lib.mkDefault true;
  # networking.interfaces.eno0.useDHCP = lib.mkDefault true;
  # networking.interfaces.wlp2s0.useDHCP = lib.mkDefault true;

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
#+end_src
** Empirica
*** README.md
#+begin_src markdown :tangle hosts/empirica/README.md
# Empirica

Mini PC running all self-hosted services

## Specifications
- Model: Lenovo M910Q
- Purpose: Hosting all my home server stack
- Headless NixOS Server
#+end_src
*** configuration.nix
Empirica runs all my [[https://joshblais.com/posts/selfhosting/][selfhosted services to get me free from subscriptions]]

#+begin_src nix :tangle hosts/empirica/configuration.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/cli-tui
    ../../modules/shared
    ../../modules/security
    ../../modules/services
    # ... other imports
  ];

  # Host-specific configuration
  networking.hostName = "empirica";
  # ... rest of your host config
}
#+end_src
*** hardware-configuration.nix
#+begin_src nix :tangle hosts/empirica/hardware-configuration.nix
# This is a placeholder hardware configuration
# You'll need to generate the real one when you install NixOS
{ config, lib, pkgs, modulesPath, ... }:
{
  imports = [ ];

  # Placeholder hardware config
  boot.initrd.availableKernelModules = [ "xhci_pci" "ahci" "nvme" "usb_storage" "sd_mod" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-intel" ];
  boot.extraModulePackages = [ ];

  # Placeholder filesystem config
  fileSystems."/" = {
    device = "/dev/disk/by-uuid/placeholder";
    fsType = "ext4";
  };

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
#+end_src

** Alexandria
*** README.md
#+begin_src markdown :tangle hosts/alexandria/README.md
# Alexandria

Hetzner server database host

## Specifications
#+end_src
*** configuration.nix
#+begin_src nix :tangle hosts/alexandria/configuration.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/security
    ../../modules/services
  ];

  networking.hostName = "alexandria";

  nix.settings.experimental-features = [ "nix-command" "flakes" ];

  boot.loader.grub = {
    enable = true;
    devices = [ "/dev/sda" ];
  };

  users.users.joshua = {
    isNormalUser = true;
    description = "Joshua Blais";
    group = "joshua";
    extraGroups = [ "networkmanager" "wheel" ];
  };

  users.groups.joshua = {};

  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

  environment.systemPackages = with pkgs; [
    vim
    git
  ];

  system.stateVersion = "24.11";
}
#+end_src

*** hardware-configuration.nix
#+begin_src nix :tangle hosts/alexandria/hardware-configuration.nix
# This is a placeholder hardware configuration
# You'll need to generate the real one when you install NixOS
{ config, lib, pkgs, modulesPath, ... }:
{
  imports = [ ];

  # Placeholder hardware config
  boot.initrd.availableKernelModules = [ "xhci_pci" "ahci" "nvme" "usb_storage" "sd_mod" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-intel" ];
  boot.extraModulePackages = [ ];

  # Placeholder filesystem config
  fileSystems."/" = {
    device = "/dev/disk/by-uuid/placeholder";
    fsType = "ext4";
  };

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
#+end_src
** Empire
*** README.md
#+begin_src markdown :tangle hosts/empire/README.md
# Empire

Hetzner server running all containers currently -

TODO: migrate all services to nixOS.
## Specifications
#+end_src
*** configuration.nix
#+begin_src nix :tangle hosts/empire/configuration.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./hardware-configuration.nix
    ../../modules/security
  ];

  networking.hostName = "empire";

  nix.settings.experimental-features = [ "nix-command" "flakes" ];

  boot.loader.grub = {
    enable = true;
    devices = [ "/dev/sda" ];
  };

  users.users.joshua = {
    isNormalUser = true;
    description = "Joshua Blais";
    group = "joshua";
    extraGroups = [ "networkmanager" "wheel" ];
  };

  users.groups.joshua = {};

  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

  environment.systemPackages = with pkgs; [
    vim
    git
  ];

  system.stateVersion = "24.11";
}
#+end_src
*** hardware-configuration.nix
#+begin_src nix :tangle hosts/empire/hardware-configuration.nix
# This is a placeholder hardware configuration
# You'll need to generate the real one when you install NixOS
{ config, lib, pkgs, modulesPath, ... }:
{
  imports = [ ];

  # Placeholder hardware config
  boot.initrd.availableKernelModules = [ "xhci_pci" "ahci" "nvme" "usb_storage" "sd_mod" ];
  boot.initrd.kernelModules = [ ];
  boot.kernelModules = [ "kvm-intel" ];
  boot.extraModulePackages = [ ];

  # Placeholder filesystem config
  fileSystems."/" = {
    device = "/dev/disk/by-uuid/placeholder";
    fsType = "ext4";
  };

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
}
#+end_src

** Citadel
* Modules
** Home-manager
I use [[https://github.com/nix-community/home-manager][home-manager]] to manage all dotfiles that require customization. I don't agree with rewriting my configs when you can just symlink them using ~mkOutOfStoreSymlink~
*** default.nix
#+begin_src nix :tangle modules/home-manager/default.nix
{ config, lib, pkgs, inputs, ... }:

let
  # Create a function to make out-of-store symlinks
  mkOutOfStoreSymlink = path: config.lib.file.mkOutOfStoreSymlink path;
in
{
  imports = [
    ./git.nix
    ./setup.nix
    ./theming.nix
    ./browsers.nix
  ];

  home.username = "joshua";
  home.homeDirectory = "/home/joshua";
  home.stateVersion = "25.05";

  # Let home-manager manage itself
  programs.home-manager.enable = true;

   # XDG MIME associations for file types
  xdg.mimeApps = {
    enable = true;
    defaultApplications = {
      "video/mp4" = "mpv.desktop";
      "video/x-msvideo" = "mpv.desktop";
      "video/x-matroska" = "mpv.desktop";
      "video/webm" = "mpv.desktop";
      "video/quicktime" = "mpv.desktop";
      "audio/mpeg" = "mpv.desktop";
      "audio/flac" = "mpv.desktop";
      "audio/ogg" = "mpv.desktop";
      "audio/wav" = "mpv.desktop";
      "image/jpeg" = "feh.desktop";
      "image/png" = "feh.desktop";
      "image/gif" = "feh.desktop";
      "application/pdf" = "org.pwmt.zathura.desktop";
      "application/vnd.openxmlformats-officedocument.wordprocessingml.document" = "libreoffice-writer.desktop";
      "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet" = "libreoffice-calc.desktop";
    };
  };

  # Use mkOutOfStoreSymlink for live editing
  home.file = {
    ".config/doom".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/doom";
    ".zshrc".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/zsh/.zshrc";
    ".config/starship.toml".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/starship/starship.toml";
    ".config/tmux/plugins".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/tmux/plugins";
    ".config/hypr".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/hypr";
    ".config/kitty".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/kitty";
    ".config/waybar".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/waybar";
    ".config/swaync".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/swaync";
    ".config/wofi".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/wofi";
    ".config/nvim".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/nvim";
    ".config/zathura".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/zathura";
    ".tmux.conf".source = mkOutOfStoreSymlink "${config.home.homeDirectory}/nixos-config/dotfiles/tmux/.tmux.conf";
  };
}
#+end_src

*** setup.nix
Scripts to setup a new machine with directories in their correct locations.

#+begin_src nix :tangle modules/home-manager/setup.nix
{ config, pkgs, ... }:
{
  # Clone repositories - skip private repos if SSH key not available
  home.activation.cloneRepos = config.lib.dag.entryAfter ["writeBoundary"] ''
    # Check if SSH key exists and GitHub is accessible
    if ${pkgs.openssh}/bin/ssh -T git@github.com 2>&1 | grep -q "successfully authenticated"; then
      # Scripts repo (private - only if SSH works)
      if [ ! -d "${config.home.homeDirectory}/.config/scripts" ]; then
        echo "Cloning private scripts repo..."
        ${pkgs.git}/bin/git clone git@github.com:jblais493/scripts.git ${config.home.homeDirectory}/.config/scripts
      fi
    else
      echo "SSH key not configured for GitHub, skipping private scripts repo"
    fi

    # Public repos
    if [ ! -d "${config.home.homeDirectory}/Pictures/Wallpapers" ]; then
      ${pkgs.git}/bin/git clone https://github.com/jblais493/Wallpapers ${config.home.homeDirectory}/Pictures/Wallpapers
    fi

    if [ ! -d "${config.home.homeDirectory}/.config/kmonad" ]; then
      ${pkgs.git}/bin/git clone https://github.com/jblais493/Kmonad-thinkpad ${config.home.homeDirectory}/.config/kmonad
    fi
  '';

  programs.gpg.enable = true;
  services.gpg-agent = {
    enable = true;
    pinentry.package = pkgs.pinentry-gtk2;
    extraConfig = ''
      allow-loopback-pinentry
    '';
  };
}
#+end_#+begin_src

#+end_src
*** theming.nix
#+begin_src nix :tangle modules/home-manager/theming.nix
{ config, pkgs, ... }:
{
  # Proper cursor configuration - the NixOS way
  home.pointerCursor = {
    name = "Nordzy-cursors-white";  # This is the correct name
    package = pkgs.nordzy-cursor-theme;
    size = 24;
    gtk.enable = true;
    x11.enable = true;
  };

  gtk = {
    enable = true;
    theme = {
      name = "Nordic-darker";
      package = pkgs.nordic;
    };
    iconTheme = {
      name = "Zafiro-icons-Dark";
      package = pkgs.zafiro-icons;
    };
    cursorTheme = {
      name = "Nordzy-cursors";
      package = pkgs.nordzy-cursor-theme;
      size = 24;
    };
    gtk3.extraConfig = {
      gtk-application-prefer-dark-theme = 1;
    };
    gtk4.extraConfig = {
      gtk-application-prefer-dark-theme = 1;
    };
  };

  # Qt follows GTK - the principle of least surprise
  qt = {
    enable = true;
    platformTheme.name = "gtk";
    style = {
      name = "gtk2";
    };
  };

  # Hyprland-specific cursor fixes (since you're using Hyprland)
  wayland.windowManager.hyprland.settings = {
    exec-once = [
      "gsettings set org.gnome.desktop.interface cursor-theme 'Nordzy-cursors-white'"
      "gsettings set org.gnome.desktop.interface cursor-size 24"
    ];
  };
}
#+end_src
*** git.nix
Establish git settings

#+begin_src nix :tangle modules/home-manager/git.nix
{ config, pkgs, ... }:
{
  programs.git = {
    enable = true;
    userName = "Joshua Blais";
    userEmail = "josh@joshblais.com";

    extraConfig = {
      credential.helper = "store";
      init.defaultBranch = "master";
      core = {
        editor = "nvim";
        autocrlf = "input";
      };
      pull.rebase = true;
      push.autoSetupRemote = true;

      url."git@github.com:".insteadOf = "https://github.com/";
    };
  };
}
#+end_src
*** browsers.nix
This configures extensions and settings in firefox

#+begin_src nix :tangle modules/home-manager/browsers.nix
{ config, pkgs, ... }:
{
  programs.firefox = {
    enable = true;
    profiles.default = {
      name = "Default";
      isDefault = true;

      # Updated extension syntax
      extensions.packages = with pkgs.nur.repos.rycee.firefox-addons; [
        ublock-origin
        tridactyl
        don-t-fuck-with-paste
        decentraleyes
        privacy-badger
        istilldontcareaboutcookies
        violentmonkey
        wayback-machine
        leechblock-ng
        kristofferhagen-nord-theme
      ];

      settings = {
        # Privacy settings
        "privacy.trackingprotection.enabled" = true;
        "privacy.trackingprotection.socialtracking.enabled" = true;
        "privacy.donottrackheader.enabled" = true;

        # Disable telemetry
        "datareporting.healthreport.uploadEnabled" = false;
        "datareporting.policy.dataSubmissionEnabled" = false;
        "toolkit.telemetry.enabled" = false;

        # Security
        "dom.security.https_only_mode" = true;
        "security.tls.insecure_fallback_hosts" = "";

        # Disable autoplay
        "media.autoplay.default" = 5;

        # Performance
        "browser.cache.disk.enable" = false;
        "browser.sessionstore.privacy_level" = 2;
      };

      # Custom userChrome.css for vertical tabs
      userChrome = ''
        /* Hide horizontal tabs when using Tree Style Tab */
        #TabsToolbar {
          visibility: collapse !important;
        }

        /* Nord color scheme for Firefox UI */
        :root {
          --nord0: #2e3440;
          --nord1: #3b4252;
          --nord2: #434c5e;
          --nord3: #4c566a;
          --nord4: #d8dee9;
          --nord5: #e5e9f0;
          --nord6: #eceff4;
          --nord7: #8fbcbb;
          --nord8: #88c0d0;
          --nord9: #81a1c1;
          --nord10: #5e81ac;
        }

        /* Apply Nord colors to browser chrome */
        #navigator-toolbox {
          background-color: var(--nord1) !important;
        }

        #urlbar {
          background-color: var(--nord2) !important;
          color: var(--nord6) !important;
        }
      '';

      # Updated bookmarks syntax
      bookmarks = {
        force = true;
        settings = [
          {
            name = "Toolbar";
            toolbar = true;
            bookmarks = [
              {
                name = "NixOS Manual";
                url = "https://nixos.org/manual/nixos/stable/";
              }
              # Add your bookmarks here
            ];
          }
        ];
      };
    };
  };

  # Brave and Zen browser don't have home-manager modules yet
  # They're installed system-wide via the desktop/browsers.nix module
}
#+end_src
** Development
*** default.nix
Basic development packages for ease of development and deployment.

#+begin_src nix :tangle modules/development/default.nix
{ config, pkgs, ... }:
{
  imports = [
    ./doom.nix
  ];

  # CLI/TUI tools
  environment.systemPackages = with pkgs; [
    # Editor
    neovim
    hugo
    # Go toolchain
    go
    gopls
    godef
    gotools

    (python313.withPackages (ps: with ps; [
      tweepy
      mastodon-py
    ]))
    gcc
    gh
    glibc
    clang
    cmake
    libtool
    gnumake
    sdbus-cpp
    keychain

    # Nix related
    nixd  # Nix LSP
    direnv
    devenv

    # Rust related
    rustc
    cargo
    rustfmt
    rust-analyzer
    clippy
  ];
}
#+end_src

*** doom.nix
Emacs configuration using DOOM emacs.
#+begin_src nix :tangle modules/development/doom.nix
{ config, pkgs, ... }:
{
  # Install Emacs and Doom dependencies at system level
  environment.systemPackages = with pkgs; [
    emacs
    emacsPackages.mu4e
    # Doom Emacs dependencies
    ripgrep
    vips
    wmctrl
    fd
    imagemagick
    texlive.combined.scheme-full  # For LaTeX support
    sqlite       # For org-roam
    aspell
    aspellDicts.en
    aspellDicts.en-computers  # technical terms
    aspellDicts.en-science    # scientific terms

    # Additional tools Doom might need
    git
    curl
    wget

    # Additional Doom dependencies
    nodejs  # For LSP servers
    shellcheck  # For shell script checking
    html-tidy  # For HTML formatting
    stylelint  # For CSS linting

    # Fonts that Doom recommends
    emacs-all-the-icons-fonts
  ];

  # System-level Emacs configuration
  services.emacs = {
    enable = true;
    package = pkgs.emacs;
    defaultEditor = true;  # Set Emacs as default editor
  };
}
#+end_src
*** TODO go.nix
*** TODO python.nix
*** TODO postgres.nix
** CLI/TUI
*** default.nix
Default shell utilities across machines.

#+begin_src nix :tangle modules/cli-tui/default.nix
{ config, pkgs, ... }:

{
  imports = [
    ./zsh.nix
    ./tmux.nix
  ];

  # Tools that don't need configuration - just install them
  environment.systemPackages = with pkgs; [
    # Basic CLIs
    eza              # Better ls
    zoxide           # Better cd
    yazi             # Terminal file manager
    bat              # Better cat
    fd               # Better find
    ripgrep-all      # Better grep
    fzf              # Fuzzy finder
    wget
    curl
    killall
    zip
    unzip
    jq
    rsync
    borgbackup
    coreutils
    ydotool
    gnupg
    age
    tree

    # Nix workflow
    direnv

    # Git tools
    lazygit          # Git TUI

    # Pass
    (pass-wayland.withExtensions (exts: with exts; [
        pass-otp
        pass-import
        pass-audit
    ]))

    # System tools
    btop             # Better top
    trash-cli        # Safe rm
    tldr             # Simplified man pages
    fastfetch        # System info

    # Development containers
    podman           # Container runtime
    podman-compose   # Docker-compose for podman
  ];

  # Enable podman
  virtualisation.podman = {
    enable = true;
    dockerCompat = true;  # Create docker alias
    defaultNetwork.settings.dns_enabled = true;
  };
}
#+end_src
*** tmux.nix
#+begin_src nix :tangle modules/cli-tui/tmux.nix
{ config, pkgs, ... }:

{
  # System-level tmux installation
  environment.systemPackages = with pkgs; [
    tmux
    tmuxifier
  ];

  # Home-manager will handle the config via symlink
  programs.tmux.enable = true;
}
#+end_src
*** podman.nix
#+begin_src nix :tangle modules/cli-tui/podman.nix

#+end_src
*** zsh.nix
ZSH setup. TODO: enhance speed of startup and decide if we include modules from nix or just dotfiles.

#+begin_src nix :tangle modules/cli-tui/zsh.nix
{ config, pkgs, ... }:
{
  # Enable zsh with built-in configuration
  programs.zsh = {
    enable = true;
    ohMyZsh = {
      enable = true;
      plugins = [ "git" "sudo" "history" "fzf" ];
      theme = "robbyrussell";
    };
    autosuggestions.enable = true;
    syntaxHighlighting.enable = true;

    # Remove this line - it doesn't exist
    # enableFzfIntegration = true;

    # Optional: Add some aliases
    shellAliases = {
      ll = "ls -l";
      la = "ls -la";
      grep = "grep --color=auto";
    };
  };

  # Set zsh as default shell for user
  users.users.joshua.shell = pkgs.zsh;

  # Install additional tools
  environment.systemPackages = with pkgs; [
    starship  # Modern prompt
    fzf       # Fuzzy finder
    fd        # Better find (used by fzf)
    ripgrep   # Better grep (used by fzf)
  ];
}
#+end_src

** Desktop
*** default.nix
This declares all desktop modules for importing to desktop/laptops.

#+begin_src nix :tangle modules/desktop/default.nix
{ config, pkgs, ... }:
{
  imports = [
    ./hyprland.nix
    ./kmonad.nix
    ./fonts.nix
    ./audio.nix
    ./bluetooth.nix
    ./printing.nix
    ./browsers.nix
    ./applications.nix
    ./email.nix
    ./display-manager.nix
    ./storage.nix
    ./theming.nix
    ./boot.nix
    ./power.nix
  ];
}
#+end_src
*** hyprland.nix (system module)
#+begin_src nix :tangle modules/desktop/hyprland.nix
{ config, pkgs, ... }:

{
  # Enable Hyprland at system level
  programs.hyprland = {
    enable = true;
    xwayland.enable = true;
  };

  # Install Hyprland ecosystem packages
  environment.systemPackages = with pkgs; [
    waybar
    wtype
    wofi
    swww
    grim
    slurp
    wl-clipboard
    hyprlock
    hypridle
    hyprpicker
    hyprutils
    hyprsunset
    wlsunset
    hyprwayland-scanner
    swaynotificationcenter
    kitty
  ];
}
#+end_src

*** kmonad.nix
Kmonad setup for keyboard layout using Colemak DH and various enhancements.

#+begin_src nix :tangle modules/desktop/kmonad.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    kmonad
  ];

  # Setup Kmonad
  boot.kernelModules = [ "uinput" ];

  services.udev.extraRules = ''
    # KMonad user access to /dev/uinput
    KERNEL=="uinput", MODE="0660", GROUP="input", TAG+="uaccess"
  '';

  # Add your user to the input group
  users.users.joshua.extraGroups = [ "input" ];
}
#+end_src
*** fonts.nix
Font setup, Alegreya for reading, Geist for monospace, montserrat for professional.

#+begin_src nix :tangle modules/desktop/fonts.nix
{ config, pkgs, ... }:
{
  fonts = {
    packages = with pkgs; [
      alegreya
      # New nerd-fonts namespace
      nerd-fonts.geist-mono
      montserrat
      # Add any other fonts you need
    ];

    # Font configuration
    fontconfig = {
      enable = true;
      defaultFonts = {
        serif = [ "Alegreya" ];
        sansSerif = [ "Montserrat" ];
        monospace = [ "GeistMono Nerd Font" ];
      };
    };
  };
}
#+end_src
*** audio.nix
Piplewire, alsa, other minor audio tweaks

#+begin_src nix :tangle modules/desktop/audio.nix
{ config, pkgs, ... }:
{
  # Disable PulseAudio in favor of PipeWire
  services.pulseaudio.enable = false;

  # Enable real-time audio support
  security.rtkit.enable = true;

  # PipeWire configuration
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
    # Uncomment if you need JACK support
    # jack.enable = true;
  };

  # Audio packages
  environment.systemPackages = with pkgs; [
    pavucontrol
    playerctl
    wireplumber
  ];
}
#+end_src
*** bluetooth.nix
Bluetooth services for desktops

#+begin_src nix :tangle modules/desktop/bluetooth.nix
{ config, pkgs, ... }:
{
  # Enable Bluetooth
  hardware.bluetooth.enable = true;
  services.blueman.enable = true;

  environment.systemPackages = with pkgs; [
    bluez
    blueman
    bluez-tools
  ];
}
#+end_src
*** printing.nix
Setup printing availability for desktop machines.

#+begin_src nix :tangle modules/desktop/printing.nix
{ config, pkgs, ... }:
{
  # Enable CUPS for printing
  services.printing.enable = true;
}
#+end_src
*** browsers.nix
Brave and Firefox browsers.

#+begin_src nix :tangle modules/desktop/browsers.nix
{ config, pkgs, ... }:
{
  # System-level browser installations
  environment.systemPackages = with pkgs; [
    brave
  ];

  # Firefox with extensions via home-manager approach
  programs.firefox = {
    enable = true;

    # System-wide policies (optional)
    policies = {
      # Disable telemetry
      DisableTelemetry = true;
      DisableFirefoxStudies = true;
      DisablePocket = true;

      # Security settings
      BlockAboutConfig = false;
      OfferToSaveLogins = false;
    };
  };
}
#+end_src
*** applications.nix
Various desktop applications for comms, files, etc.

#+begin_src nix :tangle modules/desktop/applications.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    # File managers
    xfce.thunar
    xfce.tumbler

    # Communication
    signal-desktop
    telegram-desktop
    thunderbird
    element-desktop

    # Graphics
    gimp3-with-plugins

    # KDE connect
    kdePackages.kdeconnect-kde

    # Productivity
    libreoffice

    # System utilities
    brightnessctl
    libnotify
    xdg-utils
    gammastep

    # VPN
    mullvad-vpn
    mullvad

    # Others
    qbittorrent
    syncthing
    flatpak
  ];

  programs.kdeconnect.enable = true;

systemd.user.services.kdeconnect = {
    description = "KDE Connect daemon";
    wantedBy = [ "default.target" ];
    after = [ "graphical-session.target" ];
    serviceConfig = {
      ExecStart = "${pkgs.kdePackages.kdeconnect-kde}/bin/kdeconnectd";
      Restart = "on-failure";
      RestartSec = 3;
    };
    environment = {
      # Ensure proper DBus session
      DISPLAY = ":0";
    };
  };

  networking.firewall = {
    allowedTCPPortRanges = [ { from = 1714; to = 1764; } ];
    allowedUDPPortRanges = [ { from = 1714; to = 1764; } ];
  };

  # Enable flatpak
  services.flatpak.enable = true;
}
#+end_src
*** display-manager.nix
Login screen.

#+begin_src nix :tangle modules/desktop/display-manager.nix
{ config, pkgs, ... }:
{
  # Enable GDM display manager (updated syntax)
  services.xserver.enable = true;

  services.displayManager.gdm = {
    enable = true;
    wayland = true;
  };
}
#+end_src
*** storage.nix
Dealing with automatic mounting of drives, creating new ISOs etc.

#+begin_src nix :tangle modules/desktop/storage.nix
{ config, pkgs, ... }:
{
 # Enable automatic mounting of USB drives
  services.udisks2.enable = true;

  # Enable GVFS for desktop integration
  services.gvfs.enable = true;

  # Force start udisks2
  systemd.services.udisks2 = {
    wantedBy = [ "graphical-session.target" ];
  };

  # Additional packages for USB/storage management
  environment.systemPackages = with pkgs; [
    # File manager integration
    gvfs          # GNOME Virtual File System
    udisks2       # Disk management service

    # Disk management tools
    gnome-disk-utility  # Disk utility GUI with image writing
    gparted       # Partition editor

    # Image writing tools
    isoimagewriter   # Simple USB image writer
    # or alternatively:
    # gnome-multi-writer  # GNOME's USB writer
    # raspberry-pi-imager # Official Pi imager (works for other images too)

    # Command line tools
    dd_rescue     # Better dd with error recovery
    pv            # Progress viewer for dd operations
  ];
}
#+end_src
*** theming.nix
Setting nord theming and styles. Replace packages here for setting your own custom theme.

#+begin_src nix :tangle modules/desktop/theming.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    nordic
    nordzy-cursor-theme
    zafiro-icons
    papirus-icon-theme
    polkit_gnome
  ];

  programs.dconf.enable = true;
  security.polkit.enable = true;

  systemd.user.services.polkit-gnome-authentication-agent-1 = {
    description = "polkit-gnome-authentication-agent-1";
    wantedBy = [ "graphical-session.target" ];
    wants = [ "graphical-session.target" ];
    after = [ "graphical-session.target" ];
    serviceConfig = {
      Type = "simple";
      ExecStart = "${pkgs.polkit_gnome}/libexec/polkit-gnome-authentication-agent-1";
      Restart = "on-failure";
      RestartSec = 1;
      TimeoutStopSec = 10;
    };
  };
}
#+end_src

*** boot.nix
This sets up boot screens and adds logos with plymouth

#+begin_src nix :tangle modules/desktop/boot.nix
{ config, lib, pkgs, ... }:

{
  # Fast, beautiful Plymouth boot with NixOS branding
  boot.plymouth = {
    enable = true;
    theme = "breeze";
  };

  # Silent boot for speed and clean look
  boot.kernelParams = [
    "quiet"
    "splash"
    "loglevel=3"
    "rd.systemd.show_status=false"
    "rd.udev.log_level=3"
    "systemd.show_status=auto"
  ];

  boot.consoleLogLevel = 0;

  # Fast boot timeout (updated option name)
  boot.loader.timeout = 1;

  # Beautiful GRUB theme
  boot.loader.grub.theme = pkgs.nixos-grub2-theme;

  # Keep boot entries clean
  boot.loader.grub.configurationLimit = 5;

  # LUKS + Plymouth integration for themed password prompts
  boot.initrd.systemd.enable = true;
}
#+end_src

*** power.nix
#+begin_src nix :tangle modules/desktop/power.nix
{ config, lib, pkgs, ... }:

{
  # Disable conflicting power management
  services.power-profiles-daemon.enable = false;

  # Core power management
  services = {
    # Essential power monitoring
    upower = {
      enable = true;
      percentageLow = 15;
      percentageCritical = 5;
      percentageAction = 3;
      criticalPowerAction = "Hibernate";
    };

    # TLP for battery optimization
    tlp = {
      enable = true;
      settings = {
        # CPU management
        CPU_SCALING_GOVERNOR_ON_AC = "performance";
        CPU_SCALING_GOVERNOR_ON_BAT = "powersave";

        # AMD Ryzen optimizations
        CPU_ENERGY_PERF_POLICY_ON_AC = "performance";
        CPU_ENERGY_PERF_POLICY_ON_BAT = "power";

        # Basic power saving
        WIFI_PWR_ON_AC = "off";
        WIFI_PWR_ON_BAT = "on";

        # USB autosuspend
        USB_AUTOSUSPEND = 1;

        # NVMe power management
        DISK_DEVICES = "nvme0n1";
        DISK_APM_LEVEL_ON_BAT = "128";
      };
    };

    # Thermal management
    thermald.enable = true;

    # Firmware updates
    fwupd.enable = true;
  };

  # Essential tools
  environment.systemPackages = with pkgs; [
    powertop
    acpi
    lm_sensors
  ];

  # AMD-specific kernel parameter
  boot.kernelParams = [ "amd_pstate=active" ];

  # Basic power management
  powerManagement = {
    enable = true;
    powertop.enable = true;
  };

  # AMD microcode
  hardware.cpu.amd.updateMicrocode = true;
}
#+end_src
*** email.nix
Setting up email in mu4e in emacs, packages necessary:

#+begin_src nix :tangle modules/desktop/email.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    mu
    isync
    msmtp
    gnutls
  ];
}
#+end_src
** Shared
*** default.nix
#+begin_src nix :tangle modules/shared/default.nix
{ config, pkgs, inputs, ... }:
{
  imports = [
    ./networking.nix
  ];

  # Fix NUR overlay reference
  nixpkgs.overlays = [ inputs.nur.overlays.default ];

  # Common configuration for all hosts
  time.timeZone = "America/Edmonton";
  i18n.defaultLocale = "en_CA.UTF-8";

 # Enable flakes and trusted users
  nix.settings = {
    experimental-features = [ "nix-command" "flakes" ];
    trusted-users = [ "root" "joshua" ];  # Add this line
  };

  # Allow unfree packages
  nixpkgs.config.allowUnfree = true;
}
#+end_src

*** networking.nix
#+begin_src nix :tangle modules/shared/networking.nix
{ config, pkgs, ... }:
{
  # Enable NetworkManager
  networking.networkmanager.enable = true;

  # Packages
  environment.systemPackages = with pkgs; [
    networkmanager
    networkmanagerapplet
  ];
}
#+end_src

** Server
** Services
*** default.nix
#+begin_src nix :tangle modules/services/default.nix
{ config, lib, pkgs, ... }:

with lib;

let
  cfg = config.services.homelab;
in
{
  options.services.homelab = {
    enable = mkEnableOption "Enable all homelab services";

    mediaDir = mkOption {
      type = types.str;
      default = "/home/joshua/Media";
      description = "Base directory for media files";
    };

    user = mkOption {
      type = types.str;
      default = "joshua";
      description = "Main user for services";
    };

    timezone = mkOption {
      type = types.str;
      default = "America/Edmonton";
      description = "Timezone for services";
    };
  };

  config = mkIf cfg.enable {
    # Jellyfin - Native media server
    services.jellyfin = {
      enable = true;
      openFirewall = true;
      # Jellyfin will auto-detect media in common locations
    };

    # Syncthing - File synchronization
    services.syncthing = {
      enable = true;
      user = cfg.user;
      dataDir = "/home/${cfg.user}/syncthing";
      configDir = "/home/${cfg.user}/.config/syncthing";
      openDefaultPorts = true;
    };

    # The *arr stack for media management
    services.radarr = {
      enable = true;
      openFirewall = true;
    };

    services.lidarr = {
      enable = true;
      openFirewall = true;
    };

    services.prowlarr = {
      enable = true;
      openFirewall = true;
    };

    services.sonarr = {
      enable = true;
      openFirewall = true;
    };

    # Calibre-web for ebook management
    services.calibre-web = {
      enable = true;
      listen.port = 8083;
      openFirewall = true;
      options = {
        calibreLibrary = "${cfg.mediaDir}/books";
        enableBookUploading = true;
      };
    };

    # Nextcloud for cloud storage and collaboration
    services.nextcloud = {
      enable = true;
      hostName = "localhost"; # Change this to your domain
      database.createLocally = true;
      configureRedis = true;
      maxUploadSize = "16G";
      https = false; # Set to true when you have proper domain/certs
      config = {
        adminuser = "admin";
        adminpassFile = "/etc/nixos/nextcloud-admin-pass";
      };
      settings = {
        default_phone_region = "CA";
        maintenance_window_start = 2;
      };
    };

    # Create the admin password file (you'll need to create this)
    environment.etc."nextcloud-admin-pass" = {
      text = "change-this-password";
      mode = "0600";
      user = "nextcloud";
      group = "nextcloud";
    };

    # Plex (alternative to Jellyfin - enable one or the other)
    # services.plex = {
    #   enable = true;
    #   openFirewall = true;
    #   dataDir = "/var/lib/plex";
    # };

    # Create media directories with proper permissions
    systemd.tmpfiles.rules = [
      "d '${cfg.mediaDir}' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/movies' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/tvshows' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/music' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/books' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/audiobooks' 0755 ${cfg.user} users - -"
      "d '${cfg.mediaDir}/photos' 0755 ${cfg.user} users - -"
    ];

    # Services that might need container approach (for now)
    virtualisation.podman = {
      enable = true;
      dockerCompat = true;
      defaultNetwork.settings.dns_enabled = true;
    };

    # Container services for things without native NixOS support
    systemd.services = {
      # Audiobookshelf
      audiobookshelf = {
        description = "Audiobookshelf";
        after = [ "network.target" ];
        wantedBy = [ "multi-user.target" ];
        serviceConfig = {
          Type = "exec";
          User = cfg.user;
          Restart = "unless-stopped";
          ExecStart = "${pkgs.podman}/bin/podman run --rm --name audiobookshelf " +
            "-p 13378:80 " +
            "-e TZ=${cfg.timezone} " +
            "-v /home/${cfg.user}/containers/audiobookshelf/config:/config " +
            "-v /home/${cfg.user}/containers/audiobookshelf/metadata:/metadata " +
            "-v ${cfg.mediaDir}/audiobooks:/audiobooks " +
            "ghcr.io/advplyr/audiobookshelf:latest";
          ExecStop = "${pkgs.podman}/bin/podman stop audiobookshelf";
        };
      };

      # Homepage dashboard
      homepage = {
        description = "Homepage Dashboard";
        after = [ "network.target" ];
        wantedBy = [ "multi-user.target" ];
        serviceConfig = {
          Type = "exec";
          User = cfg.user;
          Restart = "unless-stopped";
          ExecStart = "${pkgs.podman}/bin/podman run --rm --name homepage " +
            "-p 3000:3000 " +
            "-e PUID=1000 -e PGID=1000 " +
            "-v /home/${cfg.user}/containers/homepage/config:/app/config " +
            "ghcr.io/gethomepage/homepage:latest";
          ExecStop = "${pkgs.podman}/bin/podman stop homepage";
        };
      };

      # Pi-hole
      pihole = {
        description = "Pi-hole DNS";
        after = [ "network.target" ];
        wantedBy = [ "multi-user.target" ];
        serviceConfig = {
          Type = "exec";
          User = "root"; # Pi-hole needs root for DNS
          Restart = "unless-stopped";
          ExecStart = "${pkgs.podman}/bin/podman run --rm --name pihole " +
            "-p 53:53/tcp -p 53:53/udp -p 8080:80 " +
            "-e TZ=${cfg.timezone} " +
            "-e WEBPASSWORD=changeme " +
            "-v /home/${cfg.user}/containers/pihole/etc-pihole:/etc/pihole " +
            "-v /home/${cfg.user}/containers/pihole/etc-dnsmasq.d:/etc/dnsmasq.d " +
            "docker.io/pihole/pihole:latest";
          ExecStop = "${pkgs.podman}/bin/podman stop pihole";
        };
      };
    };

    # Firewall ports for container services
    networking.firewall = {
      allowedTCPPorts = [ 3000 8080 13378 ];
      allowedUDPPorts = [ 53 ];
    };
  };
}
#+end_src

** Security
*** default.nix
#+begin_src nix :tangle modules/security/default.nix
{ config, pkgs, ... }:
{
  imports = [
    ./keychain.nix
    # Add other security modules here as you create them
  ];

  # Basic security hardening
  security = {
    sudo.wheelNeedsPassword = true;

    # Disable root login
    sudo.enable = true;
  };

  # SSH hardening
  services.openssh = {
    enable = true;
    settings = {
      PasswordAuthentication = false;
      PermitRootLogin = "no";
      Protocol = 2;
    };
  };

  # Firewall
  networking.firewall.enable = true;

  # Tailscale service
  services.tailscale.enable = true;

  # Security packages
  environment.systemPackages = with pkgs; [
    fail2ban
    gnupg
    age
  ];
}
#+end_src
*** fail2ban.nix
*** keychain.nix
#+begin_src nix :tangle modules/security/keychain.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    keychain
  ];
}
#+end_src
** Media
*** default.nix
#+begin_src nix :tangle modules/media/default.nix
{ config, pkgs, ... }:
{
  imports = [
    ./music.nix
    ./video.nix
    ./pdf.nix
  ];

  # Common media packages
  environment.systemPackages = with pkgs; [
    # Image viewers
    feh
    imv

    # Audio tools
    alsa-utils

    # Document viewers
    zathura
    evince
  ];
}
#+end_src
*** music.nix
#+begin_src nix :tangle modules/media/music.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    # Music players
    mpd
    mpc
    ncmpcpp
    spotify
    spotdl
    # Audio tools
    audacity
    # Audio codecs
    ffmpeg-full
    flac
    lame
  ];

  # Add the MPD service configuration
  services.mpd = {
    enable = true;
    user = "joshua";
    group = "users";
    musicDirectory = "/home/joshua/MusicOrganized/";
    # Use NixOS built-in directory management instead of custom paths
    dataDir = "/home/joshua/.config/mpd";
    playlistDirectory = "/home/joshua/.config/mpd/playlists";
    extraConfig = ''
      bind_to_address "localhost"
      port "6600"
      auto_update "yes"
      metadata_to_use "+comment"

      # PipeWire-compatible audio output
      audio_output {
          type "pipewire"
          name "PipeWire Audio"
      }

      # Fallback to pulse
      audio_output {
          type "pulse"
          name "Pulse Audio"
      }

      audio_output {
          type "fifo"
          name "album_art"
          path "/tmp/mpd.fifo"
          format "44100:16:2"
      }

      audio_output {
          type "httpd"
          name "HTTP Stream"
          encoder "vorbis"
          port "8000"
          bind_to_address "127.0.0.1"
          quality "5.0"
          format "44100:16:2"
      }
    '';
  };

  # Create the required directories
  systemd.tmpfiles.rules = [
    "d /home/joshua/.config/mpd 0755 joshua users -"
    "d /home/joshua/.config/mpd/playlists 0755 joshua users -"
  ];

  # Ensure users can access audio devices
  users.groups.audio = {};
}
#+end_src
*** video.nix
#+begin_src nix :tangle modules/media/video.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    # Video players
    mpv
    vlc

    # Video editing
    obs-studio
    shotcut

    # Video tools
    ffmpeg
    yt-dlp
  ];
}
#+end_src
*** pdf.nix
#+begin_src nix :tangle modules/media/pdf.nix
{ config, pkgs, ... }:
{
  environment.systemPackages = with pkgs; [
    # PDF viewers
    zathura
    evince
    kdePackages.okular

    # PDF tools
    poppler_utils
    pdftk

    # Document conversion
    pandoc
    texlive.combined.scheme-full
  ];
}
#+end_src
* Overlays
* Secrets
We manage secrets with [[https://github.com/ryantm/agenix][agenix]].

The workflow for adding a secret is as follows:
1. Add secret to secrets.nix - specify which keys can decrypt it
2. Create the secret: agenix -e new-secret.age
3. Rekey existing secrets if you added new machines: agenix -r
4. Deploy: Your NixOS systems will automatically decrypt the secrets they have keys for

You can retrieve Machine SSH keys by running ~cat /etc/ssh/ssh_host_ed25519_key.pub~ in any Linux machine with ssh enabled.
** secrets.nix
#+begin_src nix :tangle secrets/secrets.nix
let
  # Your personal SSH public key (from ~/.ssh/joshuakey.pub)
  joshua = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAICCWNto66rFbOvb1VDEDuZYdwHQPfKM7+EjpnHvs3eRr joshua@joshuablais.com";

  # Machine SSH host keys
  # king = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBb root@king";
  # theologica = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAICCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCc root@theologica";
  # alexandria = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDd root@alexandria";
  # empire = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEe root@empire";


  # Groups for convenience
  users = [ joshua ];
  # desktops = [ king theologica ];
  # servers = [ alexandria empire ];
  # allSystems = desktops ++ servers;
in
{
  # Database secrets (only for servers)
  "postgres-password.age".publicKeys = users;
}
#+end_src

* flake.nix
#+begin_src nix :tangle flake.nix
{
  description = "Joshua Blais' NixOS Configuration";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    nixpkgs-stable.url = "github:NixOS/nixpkgs/nixos-25.05";
    nur.url = "github:nix-community/NUR";

    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    deploy-rs = {
      url = "github:serokell/deploy-rs";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    agenix = {
      url = "github:ryantm/agenix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { self, nixpkgs, home-manager, deploy-rs, agenix, disko, ... }@inputs: {
    nixosConfigurations = {
      # Laptop hosts
      theologica = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = { inherit inputs; };
        modules = [
          ./hosts/theologica/configuration.nix
          agenix.nixosModules.default

          # Add home-manager
          home-manager.nixosModules.home-manager
          {
            home-manager.useGlobalPkgs = true;
            home-manager.useUserPackages = true;
            home-manager.users.joshua = import ./modules/home-manager;
            home-manager.extraSpecialArgs = { inherit inputs; };
            home-manager.backupFileExtension = "backup";
          }
        ];
      };

      king = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = { inherit inputs; };
        modules = [
          ./hosts/king/configuration.nix
          agenix.nixosModules.default

          # # Add home-manager
          home-manager.nixosModules.home-manager
          {
            home-manager.useGlobalPkgs = true;
            home-manager.useUserPackages = true;
            home-manager.users.joshua = import ./modules/home-manager;
            home-manager.extraSpecialArgs = { inherit inputs; };
            home-manager.backupFileExtension = "backup";
          }
        ];
      };

      axios = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = { inherit inputs; };
        modules = [
          ./hosts/axios/configuration.nix
          agenix.nixosModules.default

          # Add home-manager
          home-manager.nixosModules.home-manager
          {
            home-manager.useGlobalPkgs = true;
            home-manager.useUserPackages = true;
            home-manager.users.joshua = import ./modules/home-manager;
            home-manager.extraSpecialArgs = { inherit inputs; };
            home-manager.backupFileExtension = "backup";
          }
        ];
      };

      # Server hosts (no home-manager needed)
      alexandria = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = { inherit inputs; };
        modules = [
          ./hosts/alexandria/configuration.nix
          agenix.nixosModules.default
        ];
      };

      empire = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = { inherit inputs; };
        modules = [
          ./hosts/empire/configuration.nix
          agenix.nixosModules.default
        ];
      };
    };

    # Deploy-rs configuration for remote deployment
    deploy.nodes = {
      alexandria = {
        hostname = "alexandria.your-domain.com";
        profiles.system = {
          user = "root";
          path = deploy-rs.lib.x86_64-linux.activate.nixos self.nixosConfigurations.alexandria;
        };
      };
    };
  };
}
#+end_src

* TODO Fixes
** TODO Addin battery optimizations for laptop
** TODO speed of zsh shell opening
** TODO Get mu4e working
** TODO Hugo blog version issue
** TODO Set global secrets, explore sops-nix
** TODO Persistence of git login
